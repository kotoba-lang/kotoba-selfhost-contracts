(ns kotoba.selfhost.oracle-gen
  "Regenerate the shipped KIR from kotoba/*.kotoba.

      clojure -M:test:gen

  Runs under :test because the compiler lives there. The artifacts it writes are
  what production loads, so nothing here may transform them — same compile path
  as the authority tests, pretty-printed EDN, no post-processing. If this file
  and the tests disagreed about how to compile, the tests would be checking
  something other than what ships."
  (:require [clojure.java.io :as io]
            [clojure.pprint :as pp]
            [kotoba.compiler.core :as compiler]
            [kotoba.selfhost.oracle :as oracle])
  (:gen-class))

(defn compile-kir [source-path]
  (let [r (compiler/compile-source (slurp source-path) :wasm32-kotoba-v1 {})]
    (or (:kir r)
        (throw (ex-info "compile-source returned no :kir" {:source source-path})))))

(defn write-artifact! [id source-path]
  (let [out (io/file "resources" (oracle/resource-path id))]
    (io/make-parents out)
    (spit out (with-out-str (pp/pprint (compile-kir source-path))))
    (.getPath out)))

(defn regenerate-all! []
  (mapv (fn [[id src]] (write-artifact! id src)) (sort-by key oracle/oracles)))

(defn -main [& _]
  (run! println (regenerate-all!))
  (shutdown-agents))
