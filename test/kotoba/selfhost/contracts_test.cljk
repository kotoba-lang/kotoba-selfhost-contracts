(ns kotoba.selfhost.contracts-test
  (:require [clojure.test :refer [deftest is testing]]
            [kotoba.selfhost.contracts :as contracts]))

(deftest selfhost-seeds-load-and-validate
  (let [seeds (contracts/load-seeds)]
    (testing "all canonical seeds are present"
      (is (= (set (map keyword contracts/seed-names)) (set (keys seeds))))
      (is (= 17 (count seeds))))
    (testing "safe analyzer facts remain the shipped seed"
      (is (= "kotoba.selfhost.safe-analyzer-facts.v0"
             (:schema (:safe_analyzer_facts seeds)))))
    (testing "all seeds are valid EDN authority values"
      (is (:ok? (contracts/validate-seeds seeds)))
      (is (= [] (:kotoba.selfhost/problems (contracts/check-data seeds)))))))

(deftest validation-rejects-bad-seed-shapes
  (testing "missing authority metadata is rejected"
    (is (= [{:kotoba.selfhost/name :bad
             :kotoba.selfhost/problem :missing-schema}
            {:kotoba.selfhost/name :bad
             :kotoba.selfhost/problem :invalid-owner
             :kotoba.selfhost/value nil}
            {:kotoba.selfhost/name :bad
             :kotoba.selfhost/problem :non-edn-canonical-format
             :kotoba.selfhost/value :json}]
           (contracts/seed-problems :bad {:canonical-format :json}))))
  (testing "schema, export, and platform drift is rejected"
    (let [problems (contracts/seed-problems
                    :runtime_contract
                    {:schema "wrong"
                     :owner "kotoba/selfhost"
                     :canonical-format :edn
                     :common-exports {:bad :value}
                     :target-exports {:linux {"x" 1}}})]
      (is (some #(= :schema-name-mismatch (:kotoba.selfhost/problem %)) problems))
      (is (some #(= :invalid-common-exports (:kotoba.selfhost/problem %)) problems))
      (is (some #(= :unknown-target-platform (:kotoba.selfhost/problem %)) problems)))))
